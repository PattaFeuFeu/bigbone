plugins {
    id 'social.bigbone.bigbone-library'
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    api libs.okhttp
    api libs.gson
    implementation libs.kotlin.jdk8
    testImplementation libs.junit.jupiter
    testRuntimeOnly libs.junit.platform.launcher
    testImplementation libs.kluent
    testImplementation libs.mockk
    testImplementation libs.mockk.dsl
    integrationTestImplementation libs.junit.jupiter
    integrationTestRuntimeOnly libs.junit.platform.launcher
    integrationTestImplementation libs.junit.platform.suite.engine
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test

    useJUnitPlatform()

    testLogging {
        events "passed"
    }
}
